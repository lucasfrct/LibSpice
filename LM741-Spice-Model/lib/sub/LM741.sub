*//////////////////////////////////////////////////////////////////////
* Models developed by:
* Lucas Costa | lucasfrct@gmail.com
* Model format: SPICE3
* Model generated on May 2019
*//////////////////////////////////////////////////////////////////
* TEXAS INSTRUMENTS LM741
* GENERAL PURPOSE OPERATIONAL AMPLIFIER
*/////////////////////////////////////////////////////////////////
* TEMP RANGE = -50ºC to 125ºC
* Input Voltage Offset	V(min)	1mV
* Input Voltage 		V(in) 	15V
* Input Current 		I(in) 	500nA
* Input Resistenace 	Z(in) 	2MR (Mega Ohms)
* Input Capacitance 	X(in) 	44pF
*
* Output Voltage 		V(out) 	14V
* Output Current 		I(out) 	25mA
* Output Resistance 	Z(out) 	60R
* Gain 					G(out) 	200 V/mV
*
* DC Supply Voltage 	V(in) 	22V
* Power Comsumption 	P(c) 	100mW
* Power Discipation 	P(d) 	500mW
* Frequency				Fr 		4.5MHz
* 
* LM741 (CDIP, PDIP | 8-pin)
*					 	__________  
*		OFFSET NULL 1--[  |\      ]--8 NC
* 				-IN 2--[--| \ _   ]--7 +VSS
*				+IN 3--[--| /  |--]--6 OUT
*			   -VSS 4--[__|/______]--5 OFFSET NULL
*                       
* Equivalency LM741: LM201 | LM709 | LM748 | MC1439
*
* connections:    
*				(+IN) non-inverting input
*				|   (-IN) inverting input
*				|   |   (+VSS) positive power supply
*				|   |   |   (-VSS) negative power supply
*				|   |   |   |   (OUT) output
*				|   |   |   |   |
.subckt	LM741	3	2	7	4	6
* #ASSOC Category="Op-amps" symbol=opamp
* To use a subcircuit, the name must begin with 'X'.  For example:
Q1 N001 3 N008 0 NP
Q2 N001 2 N009 0 NP
Q5 N011 N010 N009 0 PN
Q6 N013 N010 N008 0 PN
Q7 N013 N016 N018 0 NP
Q8 N011 N016 N019 0 NP
Q3 7 N013 N016 0 NP
Q4 N001 N001 7 0 PN
R1 N018 4 1K
R2 N016 4 50K
R3 N019 4 1K
Q9 N010 N001 7 0 PN
Q10 N002 N002 7 0 PN
Q11 N003 N002 7 0 PN
Q12 N014 N014 4 0 NP
Q13 N010 N014 N020 0 NP
R4 N020 4 5K
Q14 N003 N004 N007 0 NP
Q15 N007 N015 N017 0 NP
Q16 N007 N011 N015 0 NP
R5 N015 4 50K
R6 N017 4 50
Q17 N011 N017 4 0 NP
C1 N003 N011 30p
R7 N007 N004 7.5K
R8 N003 N004 4.5K
Q18 7 N003 N005 0 NP
Q19 4 N007 N012 0 PN
R9 N005 6 25
R10 6 N012 50
Q20 N003 N005 6 0 NP
R13 N002 N014 39K
.model NP NPN(Bf=125 Cje=.5p Cjc=.5p Rb=500)
.model PN LPNP(Bf=25 Cje=.3p Cjc=1.5p Rb=250)
.ends LM741